apply from: "$rootDir/gradle/local-aar-config.gradle"
apply plugin: 'maven-publish'

publishing {
    publications {
        aar(MavenPublication) {
            project.tasks.publishAarPublicationToMavenLocal.dependsOn("assembleDebug")
            groupId = project.group
            artifactId = project.name
            version = project.version
            artifact("$buildDir/outputs/aar/${project.getName()}-debug.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { addDependencyNode(dependencies,it) }
                configurations.api.allDependencies.each { addDependencyNode(dependencies,it) }
            }
        }
    }

    repositories {
        maven {
            url = "$rootDir/localMavenRepository"
        }
        project.tasks.publishAarPublicationToMavenRepository.dependsOn("assembleDebug")

    }
}

private void addDependencyNode(Node dependencies,Dependency it) {
    if (it.group == null || it.group == project.group) {
        return
    }

    def dependency = dependencies.appendNode('dependency')
    if (it.group == rootProject.name) {
        dependency.appendNode('groupId', project.group)
        dependency.appendNode('version', project.version)
    } else {
        dependency.appendNode('groupId', it.group)
        dependency.appendNode('version', it.version)
    }
    dependency.appendNode('artifactId', it.name)
}